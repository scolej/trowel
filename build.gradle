import java.time.Instant
import java.nio.file.Paths

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'application'

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '4.6'
}

dependencies {
    implementation(
        'com.google.code.gson:gson:2.8.2',
        'org.javatuples:javatuples:1.2',
        'org.nanohttpd:nanohttpd:2.3.1',
    )
}

mainClassName = 'trowel.Main'

sourceSets {
    main {
        resources {
            srcDirs 'src/gen/resources'
        }
    }
}

task generateBuildInfo {
    File propertiesFile = file('src/gen/resources/build.properties')
    outputs.upToDateWhen { false }
    outputs.file propertiesFile
    doLast {
        def commitHash = 'git rev-parse HEAD'.execute().text
        def currentTime = Instant.now().toString()
        propertiesFile.getParentFile().mkdirs()
        propertiesFile.withWriter() { out ->
            out.println "commitHash=$commitHash"
            out.println "buildDate=$currentTime"
        }
    }
}

assemble.dependsOn 'generateBuildInfo'
