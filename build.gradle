import java.time.Instant
import java.nio.file.Paths

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'application'

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '4.5'
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'org.javatuples:javatuples:1.2'
    compile 'org.nanohttpd:nanohttpd:2.3.1'
}

mainClassName = 'trowel.Main'

sourceSets {
    main {
        resources {
            srcDirs 'src/gen/resources'
        }
    }
}

task generateBuildInfo {
    outputs.upToDateWhen { false }
    doLast {
        def commitHash = 'git rev-parse HEAD'.execute().text
        def currentTime = Instant.now().toString()
        File propertiesFile = Paths.get('src', 'gen', 'resources', 'build.properties').toFile()
        propertiesFile.getParentFile().mkdirs()
        propertiesFile.withWriter() { out ->
            out.println "commitHash=$commitHash"
            out.println "buildDate=$currentTime"
        }
    }
}

assemble.dependsOn 'generateBuildInfo'
